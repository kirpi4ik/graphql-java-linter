plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
}

group 'org.myhab.tools'
version '1.0'

repositories {
    mavenCentral()
}

signing {
    useGpgCmd()
    sign configurations.archives
}
dependencies {
    implementation 'org.apache.groovy:groovy:4.0.6'
    implementation 'com.graphql-java:graphql-java:19.2'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.yaml:snakeyaml:1.33'
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.0'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if(JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifacts {
    archives javadocJar, sourcesJar
}
publishing {
    publications {
        maven(MavenPublication) {
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from components.java
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = mavenCentralUsername
                password = mavenCentralPassword
            }
        }
    }
}

test {
    useJUnitPlatform()
}