plugins {
    id 'groovy'
    id 'maven-publish'
    id 'signing'
    id 'java'
}

group 'org.myhab.tools'
version '1.2-SNAPSHOT'

task cliJar(type: Jar) {
    manifest {
        attributes 'Main-Class': 'graphql.linter.CLIRunner'
    }
    archiveBaseName = "${project.name}-cli"
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from { configurations.runtimeClasspath.collect { it.isDirectory() ? it : zipTree(it) } }
    with jar
}

repositories {
    mavenCentral()
}

dependencies {
    implementation 'org.apache.groovy:groovy:4.0.6'
    implementation 'com.graphql-java:graphql-java:19.2'
    implementation 'org.apache.commons:commons-lang3:3.12.0'
    implementation 'org.yaml:snakeyaml:1.33'
    testImplementation 'org.spockframework:spock-core:2.3-groovy-4.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

artifacts {
    archives javadocJar, sourcesJar, cliJar
}
publishing {
    publications {
        mavenJava(MavenPublication) {
            groupId = project.group
            artifactId = rootProject.name
            version = project.version

            from project.components.java

            pom {
                name = project.name
                description = 'GraphQL schema linting tool'
                url = 'https://github.com/kirpi4ik/graphql-java-linter'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                    scm {
                        url = 'https://github.com/kirpi4ik/graphql-java-linter'
                        connection = 'scm:https://github.com/kirpi4ik/graphql-java-linter.git'
                        developerConnection = 'scm:git@github.com:kirpi4ik/graphql-java-linter.git'
                    }
                    developers {
                        developer {
                            name = 'kirpi4ik'
                            organization = 'MyHAB'
                        }
                    }
                }
            }
        }
        gpr(MavenPublication) {
            artifactId = rootProject.name + "-cli"
            artifact cliJar {
                classifier "cli"
            }
        }
    }
    repositories {
        maven {
            def releasesRepoUrl = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            def snapshotsRepoUrl = "https://s01.oss.sonatype.org/content/repositories/snapshots/"
            url = version.endsWith('SNAPSHOT') ? snapshotsRepoUrl : releasesRepoUrl
            credentials {
                username = project.findProperty("mavenCentralUsername") ?: System.getenv("MVN_CENTRAL_USERNAME")
                password = project.findProperty("mavenCentralPassword") ?: System.getenv("MVN_CENTRAL_PASSWD")
            }
        }
        maven {
            name = "GitHubPackages"
            url = uri("https://maven.pkg.github.com/kirpi4ik/graphql-java-linter")
            credentials {
                username = project.findProperty("gthUsername") ?: System.getenv("GTH_USERNAME")
                password = project.findProperty("gthPassword") ?: System.getenv("GTH_TOKEN")
            }
        }
    }
}
signing {
    useGpgCmd()
    sign publishing.publications.mavenJava
}
test {
    useJUnitPlatform()
    testLogging {
        events "passed", "skipped", "failed"
    }
}